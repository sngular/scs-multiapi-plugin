plugins {
  id 'java-library'
  id 'maven-publish'
  id 'groovy'
  id 'signing'
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

repositories {
  mavenLocal()
  mavenCentral()
}

def SCSMultiApiPluginGroupId = 'net.coru'
def SCSMultiApiPluginVersion = '4.0.0'
def SCSMultiApiPluginArtifact = 'scs-multiapi-gradle-plugin'
def SCSMultiApiPluginDevelopers = []

def addDependency(configurationName, dependency) {
  def coordinates = [
    'group': dependency.groupId.text(),
    'name': dependency.artifactId.text(),
    'version': dependency.version.text()
  ]
  dependencies {
    add configurationName, coordinates
  }
}

def effectivePomFile = file('target/effective-pom.xml')
if (effectivePomFile.file) {
  def pom = new XmlParser().parseText(file('target/effective-pom.xml').text)
  SCSMultiApiPluginGroupId = pom.groupId.text()
  SCSMultiApiPluginVersion = pom.version.text()
  SCSMultiApiPluginArtifact = pom.artifactId.text()
  pom.dependencies.dependency.each { dependency ->
    def scope = dependency.scope.text()
    if (scope == 'compile') {
      addDependency 'implementation', dependency
    }
    else if (scope == 'test') {
      addDependency 'testCompile', dependency
    }
  }
}

dependencies {
  implementation localGroovy()
  api gradleApi()
}

jar {
  manifest {
    attributes 'Implementation-Version': (version ? version : 'unknown')
  }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
  classifier = "javadoc"
  from javadoc
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        groupId = SCSMultiApiPluginGroupId
        artifactId = SCSMultiApiPluginArtifact
        version = SCSMultiApiPluginVersion
        name = 'SCS MultiApi Plugin'
        description = 'Grable Plugin for the SCS MultiApi Generator'
        url = 'https://corunet.github.io/scs-multiapi-plugin/'

        licenses {
          license {
            name = 'Mozilla Public License, Version 2.0'
            url = 'https://www.mozilla.org/en-US/MPL/2.0/'
          }
        }

        scm {
          connection = 'scm:git:git@github.com:corunet/scs-multiapi-plugin.git'
          developerConnection = 'scm:git:git@github.com:corunet/scs-multiapi-plugin.git'
          url = 'scm:git:git@github.com:corunet/scs-multiapi-plugin.git'
        }

      }
    }
  }

  repositories {
    maven {
      name = "OSSRH"
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = System.getenv("JIRAID")
        password = System.getenv("JIRAPASS")
      }
    }
  }
}

